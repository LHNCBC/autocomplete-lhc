#     The following is the standard cshrc file for the PHR
# development team.  You should explicitly source
# this first from your .cshrc and then do your own environment
# customizations.
#     You may set variables before sourcing this to change execution
# of it.

set NODE_PATH = /depot/packages/node-v6.9.1-linux-x64

set path = ($NODE_PATH/bin /bin /usr/local/bin /usr/bin /usr/sbin /sbin /etc)

# Add node_modules/.bin to the path. We can find that from `npm bin`, but that
# tasks a second or two, and slows things down.  Instead, look in pwd for the
# top-most directory in the containing a node_modules directory.
set dir = `pwd`
set node_modules_dir = ''
if ( -e "$dir/node_modules" ) then
  set node_modules_dir = "$dir/node_modules"
endif
while ( "$dir" != '/' )
  set dir = `dirname "$dir"`
  if ( -e "$dir/node_modules" ) then
    set node_modules_dir = "$dir/node_modules"
  endif
end
if ( "$node_modules_dir" != '') then
  set path = ($path $node_modules_dir/.bin)
endif

# We setup a couple things only if in an interactive shell
if ($?prompt) then

  # Maybe needed for CDE
  #stty erase ^H
  stty erase ^\?

  # Environment settings
  umask 022
  set filec
  set history=( 2000 "%h %Y-%W-%D %T %R\n" )
  set host = `uname -n`
  #set nobeep
  #set prompt = "${user}@${host}[\!]-> "
  set type
  unset autologout
endif

# Set LD_LIBRARY_PATH, which is needed for the protrator tests while we are
# using Chrome instead of Firefox, which are doing because of this issue:
#   https://github.com/angular/protractor/issues/3200
setenv LD_LIBRARY_PATH /opt/google/chrome/lib
